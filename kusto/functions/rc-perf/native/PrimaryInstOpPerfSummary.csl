.create-or-alter function with (docstring = "[t:string, paritionId:string, percentileArray:dynamic]
        where t=table name, paritionId=Parition Id, and percentiles = dynamic array of percentile. 
        Return percentile of primary service level operation.
        Need custom trace LR.InstrumentOperation", folder = "rc-perf/native")
    PrimaryInstOpPerfSummary(t:string, partitionId:string, percentileArray:dynamic) {
    let summary = PrimaryInstOpPerfTable(t, partitionId)
    | summarize 
        percentiles_array(InstOpApplyTimeMSec, percentileArray),
        percentiles_array(InstOpReplicateTimeMSec, percentileArray),
        percentiles_array(InstOpTotalTimeMSec, percentileArray)
    | extend Percentiles = percentileArray
    | project Result = zip(Percentiles, 
                           percentiles_InstOpApplyTimeMSec, 
                           percentiles_InstOpReplicateTimeMSec, 
                           percentiles_InstOpTotalTimeMSec)
    | mv-expand Result
    | project 
        Percentiles = Result[0], 
        percentiles_InstOpApplyTimeMSec = Result[1],
        percentiles_InstOpReplicateTimeMSec = Result[2],
        percentiles_InstOpTotalTimeMSec = Result[3];
    summary
}
