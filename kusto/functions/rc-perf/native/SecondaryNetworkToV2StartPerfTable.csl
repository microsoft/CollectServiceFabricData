.create-or-alter function with (docstring = "[t:string, paritionId:string, nodeName:string]
        where t=table name, paritionId=Parition Id, nodeName=node name. 
        Return latency performance table between network 
        receive to v2 start of given node and partition.
        Need to turn on RE.SecondaryReceiveBatch", folder = "rc-perf/native")
    SecondaryNetworkToV2StartPerfTable(t:string, partitionId:string, nodeName:string) {
    let traces = GetTraces(t, partitionId, nodeName);
    let v1SecondaryReceiveTraces = traces
    | where Type == strcat("RE.SecondaryReceiveBatch@",partitionId)
    | parse Text with * "[" BeginLSN:long ", " EndLSN:long "]" *
    | extend LSN = range(BeginLSN, EndLSN)
    | mv-expand LSN
    | project v1SecondaryReceiveTimestamp = Timestamp, LSN, LSNString = tostring(LSN);
    let v2StartTraces = traces
    | where Type == strcat("LR.AcceptSingleOperationTransaction@",partitionId)
    | parse Text with * "TxId:"TransactionId:long " Lsn: " LSN:long
    | project v2StartTimestamp = Timestamp, TransactionId, LSN, LSNString = tostring(LSN);
    let secondaryNetworkToV2StartPerfTable = v2StartTraces
    | join kind=inner v1SecondaryReceiveTraces on LSNString
    | project TransactionId, LSN, v1SecondaryReceiveTimestamp, v2StartTimestamp
    | extend 
        NetworkToV2StartTimeMSec = datetime_diff('Millisecond', v2StartTimestamp, v1SecondaryReceiveTimestamp);
    secondaryNetworkToV2StartPerfTable
}
