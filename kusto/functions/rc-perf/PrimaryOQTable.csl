.create-or-alter function with (docstring = "[t:string, paritionId:string]
        where t=table name, paritionId=Parition Id,.
        Return Primary Operation Queue status on every Enqueue. 
        Need to turn on the following traces: OQ.Enqueue, OQ.OperationCommit, OQ.OperationComplete, and OQ.OperationCleanup", folder = "rc-perf")
    PrimaryOQTable(t:string, partitionId:string) {
    let primaryTraces = GetPrimaryTraces(t, partitionId);
    let primaryEnqueue = primaryTraces
    | where Type == strcat("OQ.Enqueue@", partitionId)
    | where Text has "Queue Primary-REPL-"
    | parse Text with 
        "Queue Primary-REPL-" * "[" FirstAvailableCompleted:long ", " FirstAvailableCommitted:long ", " LastCommitted:long ", " LastEnqueued:long "]: Enqueue " LSN:long "(" *;
    let primaryOpeartionCommit = primaryTraces
    | where Type == "OQ.OperationCommit"
    | parse Text with LSN:long " is committed after " CommitTimeMs:long " ms"
    | project LSN, PID, CommitTimeMs;
    let primaryOperationComplete = primaryTraces
    | where Type == "OQ.OperationComplete"
    | parse Text with LSN:long " is completed after " CompleteTimeMs:long " ms"
    | project LSN, PID, CompleteTimeMs;
    let primaryOperationCleanup = primaryTraces
    | where Type == "OQ.OperationCleanup"
    | parse Text with LSN:long " is cleaned up after " CleanupTimeMs:long " ms"
    | project LSN, PID, CleanupTimeMs;
    let primaryOQTable = primaryEnqueue
    | join kind = inner primaryOpeartionCommit on LSN, PID
    | join kind = inner primaryOperationComplete on LSN, PID
    | join kind = inner primaryOperationCleanup on LSN, PID
    | project 
        P_EnqueueTimestamp = Timestamp, 
        LSN, 
        P_FirstAvailableCompleted = FirstAvailableCompleted, 
        P_FirstAvailableCommitted = FirstAvailableCommitted, 
        P_LastCommitted = LastCommitted, 
        P_LastEnqueued = LastEnqueued, 
        P_CompletedOps = FirstAvailableCommitted - FirstAvailableCompleted,
        P_CommittedOps = LastCommitted - FirstAvailableCommitted,
        P_PendingOps = LastEnqueued - LastCommitted,
        P_CommitTimeMs = CommitTimeMs,
        P_CompleteTimeMs = CompleteTimeMs,
        P_CleanupTimeMs = CleanupTimeMs
    // Since lower may contain traces on copy context and copy in addition to replication for perf test
    | where LSN > 4;
    primaryOQTable
}
