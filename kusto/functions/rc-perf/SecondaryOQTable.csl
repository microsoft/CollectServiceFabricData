.create-or-alter function with (docstring = "[t:string, paritionId:string]
        where t=table name, paritionId=Parition Id, and nodeName=node name.
        Return Operation Queue status on every Enqueue 
        and associated commit, complete, and cleanup time.
        Need to turn on the following traces: OQ.Enqueue, OQ.OperationCommit, OQ.OperationComplete, and OQ.OperationCleanup", folder = "rc-perf")
    SecondaryOQTable(t:string, partitionId:string, nodeName:string) {
    let secondaryTraces = GetTraces(t, partitionId, nodeName);
    let secondaryEnqueue = secondaryTraces
    | where Type == strcat("OQ.Enqueue@", partitionId)
    | where Text has "Queue Secondary-REPL-"
    | parse Text with 
        "Queue Secondary-REPL-" * 
        "[" FirstAvailableCompleted:long ", " FirstAvailableCommitted:long ", " LastCommitted:long ", " LastEnqueued:long "]: Enqueue " LSN:long "(" *;
    let secondaryOpeartionCommit = secondaryTraces
    | where Type == "OQ.OperationCommit"
    | parse Text with LSN:long " is committed after " CommitTimeMs:long " ms"
    | project LSN, PID, CommitTimeMs;
    let secondaryOperationComplete = secondaryTraces
    | where Type == "OQ.OperationComplete"
    | parse Text with LSN:long " is completed after " CompleteTimeMs:long " ms"
    | project LSN, PID, CompleteTimeMs;
    let secondaryOperationCleanup = secondaryTraces
    | where Type == "OQ.OperationCleanup"
    | parse Text with LSN:long " is cleaned up after " CleanupTimeMs:long " ms"
    | project LSN, PID, CleanupTimeMs;
    let secondaryOQTable = secondaryEnqueue
    | join kind = inner secondaryOpeartionCommit on LSN, PID
    | join kind = inner secondaryOperationComplete on LSN, PID
    | join kind = inner secondaryOperationCleanup on LSN, PID
    | project 
        S_EnqueueTimestamp = Timestamp,
        LSN,
        S_FirstAvailableCompleted = FirstAvailableCompleted,
        S_FirstAvailableCommitted = FirstAvailableCommitted,
        S_LastCommitted = LastCommitted,
        S_LastEnqueued = LastEnqueued,
        S_CompletedOps = FirstAvailableCommitted - FirstAvailableCompleted,
        S_CommittedOps = LastCommitted - FirstAvailableCommitted,
        S_PendingOps = LastEnqueued - LastCommitted,
        S_CommitTimeMs = CommitTimeMs,
        S_CompleteTimeMs = CompleteTimeMs,
        S_CleanupTimeMs = CleanupTimeMs
    // Since lower LSN may contain traces on copy context and copy in addition to replication
    | where LSN > 5;
    secondaryOQTable
}
