.create-or-alter function with (docstring = "[t:string, paritionId:string, nodeName:string]
        where t=table name, paritionId=Parition Id, nodeName=node name. 
        Return latency performance table between network 
        receive to v1 apply of given node and partition.
        Need to turn on RE.SecondaryReceiveBatch", folder = "rc-perf")
    SecondaryNetworkToV1ApplyPerfTable(t:string, partitionId:string, nodeName:string) {
    let traces = GetTraces(t, partitionId, nodeName);
    let v1SecondaryReceiveTraces = traces
    | where Type == strcat("RE.SecondaryReceiveBatch@",partitionId)
    | parse Text with * "[" BeginLSN:long ", " EndLSN:long "]" *
    | extend LSN = range(BeginLSN, EndLSN)
    | mv-expand LSN
    | project v1SecondaryReceiveTimestamp = Timestamp, LSN, LSNString = tostring(LSN);
    let v1Apply = traces
    | where Type == strcat("RE.SecondarySendAcknowledgement@",partitionId)
    | parse Text with * ":" PortNumber:long ": " ReceiveLSN:long "." ApplyLSN:long ":" *
    | project v1ApplyTimestamp = Timestamp, LSN = ApplyLSN, LSNString = tostring(ApplyLSN);
    let SecondaryNetworkToV1ApplyPerfTable = v1Apply
    | join kind=inner v1SecondaryReceiveTraces on LSNString
    | project LSN, v1ApplyTimestamp, v1SecondaryReceiveTimestamp
    | extend 
        NetworkToV1ApplyTimeMSec = datetime_diff('Millisecond', v1ApplyTimestamp, v1SecondaryReceiveTimestamp);
    SecondaryNetworkToV1ApplyPerfTable
}
