.create-or-alter function with (docstring = "[R:string] where R=RepairJobId, [S:string] where S=SubscriptionId, [D:string] where D=RepairJobDatestamp.  function to display original PUT request related to Repair Job.", folder = "cluster")
    FindRepairJob(R:string, S:string, D:datetime) {
        //let R = "RM/Azure/TenantUpdate/cf54b316-eb6d-431f-a738-3841a09a9bf9/0/1959";
        //let S = "2d916e4b-990a-4017-b786-5b876c7669be";
        let Dt = D;
        let E = datetime_add("Day", 2, Dt);
        let vJobId = tolower(tostring(extract(".*([a-fA-F0-9]{8}-[a-fA-F0-9]{4}-[a-fA-F0-9]{4}-[a-fA-F0-9]{4}-[a-fA-F0-9]{12}).*", 1, R)));
        let vOperationId = toguid(toscalar(cluster('azcrp.kusto.windows.net').database('crp_allprod').ContextActivity
        | where subscriptionId == subscriptionId
        | where PreciseTimeStamp between ((Dt - 2d) .. (E + 2d))
        | where callerName == "GetLatestUpdateTenantJob"
        | where message has vJobId
        | summarize StartTime=min(PreciseTimeStamp), EndTime=max(PreciseTimeStamp) by activityId
        | project operationId=activityId));
        let vCorrelationId = toguid(toscalar(cluster('azcrp.kusto.windows.net').database('crp_allprod').ApiQosEvent_nonGet
        | where operationId == vOperationId
        | where PreciseTimeStamp between ((Dt - 2d) .. (E + 2d))
        | project correlationId
        | summarize by correlationId));
        cluster('armprod.kusto.windows.net').database('ARMProd').HttpIncomingRequests
        | where correlationId == vCorrelationId
        | where PreciseTimeStamp between ((Dt - 2d) .. (E + 2d))
        | where targetUri !has "microsoft.insights"
        | where httpMethod in ("PUT","POST","PATCH","DELETE")
        | project PreciseTimeStamp, ActivityId, subscriptionId, correlationId, principalOid, tenantId,
        authorizationSource, authorizationAction, operationName, httpMethod, hostName, targetUri, userAgent, clientRequestId,
        clientSessionId, clientIpAddress, clientApplicationId, apiVersion, serviceRequestId, durationInMilliseconds, httpStatusCode,
        exceptionMessage, errorCode, failureCause, errorMessage, commandName, parameterSetName, armServiceRequestId,
        targetResourceProvider, targetResourceType
        | summarize StartTime=min(PreciseTimeStamp), EndTime=max(PreciseTimeStamp), principalOid=max(principalOid),
        tenantId=max(tenantId), authorizationSource=max(authorizationSource), authorizationAction=max(authorizationAction),
        clientSessionId=max(clientSessionId), clientApplicationId=max(clientApplicationId), serviceRequestId=max(serviceRequestId),
        durationInMilliseconds=max(durationInMilliseconds), httpStatusCode=max(httpStatusCode), exceptionMessage=max(exceptionMessage),
        errorCode=max(errorCode), failureCause=max(failureCause), errorMessage=max(errorMessage), armServiceRequestId=max(armServiceRequestId)
        by subscriptionId, correlationId, operationName, httpMethod, hostName, targetUri, userAgent, clientRequestId,
        clientIpAddress, apiVersion, commandName, parameterSetName, targetResourceProvider, targetResourceType
        | project-reorder StartTime, EndTime
}
