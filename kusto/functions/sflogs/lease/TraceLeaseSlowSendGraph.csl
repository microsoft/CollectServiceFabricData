.create-or-alter function with (docstring = "[T:string] where T=table name. function to graph Lease.SlowSendCompletion at lease layer", folder = "sflogs/lease")
    TraceLeaseSlowSendGraph(T:string) {
    //https://eng.ms/docs/cloud-ai-platform/azure/azure-core-compute/base-platform-bburns/service-fabric-service/service-fabric-service/tsgs-and-traces-by-component/federation/lease-failure
    let pattern_Connection = @'Connection\s([a-fA-F0-9]*)\sto\s([0-9]{1,3}\.[0-9]{1,3}\.[0-9]{1,3}\.[0-9]{1,3})\:\d{4}\sat\s([0-9]{1,3}\.[0-9]{1,3}\.[0-9]{1,3}\.[0-9]{1,3})';
    let pattern_Latency = @'latency\s=\s(\d*)';
    let pattern_errorDetail = @'errorDetail\":(.*)';
    table(T)
    | where Type contains "Lease.SlowSendCompletion"
    | extend ConnectionId = extract(pattern_Connection, 1, Text, typeof(string))
    | extend ToIp = extract(pattern_Connection, 2, Text, typeof(string))
    | extend FromIp = extract(pattern_Connection, 3, Text, typeof(string))
    | extend Latency = extract(pattern_Latency, 1, Text, typeof(long))
    | extend ToFrom = strcat(ToIp,"-",FromIp)
    | summarize avg(Latency) by xtime=bin(Timestamp,5s), ToIp 
    | order by ToIp
    | render timechart 
}
