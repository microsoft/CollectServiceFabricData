.create-or-alter function with (docstring = "[T:string] where T=table name. function to display sflog table summary information", folder = "sflogs")
    TraceSummary(T:string) {
        union
                ( print Property='Ingested' , Value=tostring(toscalar(table(T) | top 1 by Timestamp asc | project ingestion_time()))),
                ( print Property='Records' , Value=tostring(toscalar(table(T) | count))),
                ( print Property='Start Time Utc' , Value=tostring(toscalar(table(T) | top 1 by Timestamp asc | project Timestamp))),
                ( print Property='End Time Utc' , Value=tostring(toscalar(table(T) | top 1 by Timestamp desc | project Timestamp))),
                ( print Property='StartVersion' , Value=tostring(toscalar(table(T) | top 1 by Timestamp asc | extend V = extract(@'_(?P<ver>[0-9.]+?)_', 1, RelativeUri, typeof(string))| distinct V))),
                ( print Property='EndVersion' , Value=tostring(toscalar(table(T) | top 1 by Timestamp desc | extend V = extract(@'_(?P<ver>[0-9.]+?)_', 1, RelativeUri, typeof(string)) | distinct V))),
                ( print Property='Nodes' , Value=tostring(toscalar(table(T) | distinct NodeName | count))),
                ( print Property='Errors' , Value=tostring(toscalar(table(T) | where Level contains "error" | count))),
                ( print Property='Warnings' , Value=tostring(toscalar(table(T) | where Level contains "warning" | count))),
                ( print Property='Exceptions' , Value=tostring(toscalar(table(T) | where Type contains "exception" or Text contains "exception" | count))),
                ( print Property='RollingBack' , Value=tostring(toscalar(table(T) | where Text contains "completedrollback" or Text contains "rollingback" | count))),
                ( print Property='ReverseProxy' , Value=tostring(iif(toscalar(table(T) | where Type contains "reverseProxy" | count) > 0, "true", "false"))),
                ( print Property='POA' , Value=tostring(iif(toscalar(table(T) | where Type contains "POA" | count) > 0, "true", "false"))),
                ( print Property='Infrastructure' , Value=tostring(iif(toscalar(table(T) | where Type contains "infrastructureservice." | count) > 0, "true", "false"))),
                ( print Property='Dns' , Value=tostring(iif(toscalar(table(T) | where Type contains "dns." | count) > 0, "true", "false"))),
                ( print Property='CrashDumps' , Value=tostring(iif(toscalar(table(T) | where Type contains "FabricDCA.WindowsFabric_ServiceFabricCrashDump" | count) > 0, "true", "false"))),
                ( print Property='Containers' , Value=tostring(iif(toscalar(table(T) | where Type contains "Hosting.ContainerActivatorServiceAgent" and Text contains "ActivateContainerRequest" | count) > 0, "true", "false"))),
                ( print Property='CRM' , Value=tostring(iif(toscalar(table(T) | where Type contains "CRM.ResourceGovernanceStatistics" and Text matches regex "GovernedServicePackages=[^0]" | count) > 0, "true", "false")))
    }
