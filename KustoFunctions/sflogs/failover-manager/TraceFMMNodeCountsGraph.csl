.create-or-alter function with (docstring = "function to graph FMM.NodeCounts at default 5 second intervals",folder = "sflogs/failover-manager") 
    TraceFMMNodeCountsGraph(T:string) {
        table(T)
        | where Type contains "FMM.NodeCounts"
        | order by Timestamp asc 
        | project Timestamp,Text
        | parse kind=regex flags=U Text with * 
            @'Nodes=' nodes
            @'\W+?Up=' up 
            @'\W+?Down=' down 
            @'\W+?DeactivatedNodes=' deactivated 
            @'\W+?Pause=' pause 
            @'\W+?Restart=' restart 
            @'\W+?RemoveData=' removeData 
            @'\W+?RemoveNode=' removeNode 
            @'\W+?Unknown=' unknown
            @'\W+?RemovedNodes=' removed 
            @'\W+?PendingDeactivateNode=' pendingDeactivate 
            @'\W+?PendingFabricUpgrade=' pendingUpgrade
        | summarize 
            avg(toint(nodes)), 
            avg(toint(up)), 
            avg(toint(down)), 
            avg(toint(deactivated)),
            avg(toint(pause)),
            avg(toint(restart)),
            avg(toint(removeData)),
            avg(toint(removeNode)),
            avg(toint(unknown)),
            avg(toint(removed)),
            avg(toint(pendingDeactivate)),
            avg(toint(pendingUpgrade))
            by bin(Timestamp,5s)
        | render timechart
    }
