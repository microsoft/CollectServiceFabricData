// todo: in progress
.create-or-alter function with (docstring = '[T:string] where T=table name. function to enumerate PLB reconfiguration scheduling reasons.', folder = 'sflogs/reconfiguration')
    TraceReconfigurationPLB (T:string) {
        table(T)
        | where Type contains 'PLB.SchedulerAction@'
        | extend serviceId = extract(@"ServiceDomainId: (?P<serviceId>\S+)", 1, Text)
        | extend decisionId = extract(@"DecisionId: (?P<decisionId>\S+)", 1, Text)
        | extend affects = split(extract(@"Affects Services with Metrics: \{ (?P<affects>.+?) \}", 1, Text), " ")
        | extend placement = extract(@"\t\tPlacement was scheduled because:.*?(?P<placement>.*?)\t\t\t\t", 1, Text)
        | extend placementList = split(replace(@"(\t|^\s)","",placement),'.')
        | extend balancing = extract(@"\t\tBalance Checking was scheduled because:.*?(?P<balancing>.*)\t\t\t", 1, Text)
        | extend constraint = extract(@"\t\tConstraint Violation Checking was scheduled because:.*?(?P<constraint>.*?)\t\t\t\t", 1, Text)
        | where isnotempty(placement) or isnotempty(balancing) or isnotempty(constraint)
        | project serviceId, decisionId, affects, placement, balancing, constraint, placementList //Text
    }


// 
//  ServiceDomainId: cfb627b6-c6a8-4978-bb7e-0e5730d62f01_PrimaryCount
//	DecisionId: 260c3bf4-870e-4332-9753-be41b5122882
//	Affects Services with Metrics: { Count PrimaryCount ReplicaCount servicefabric:/_CpuCores servicefabric:/_MemoryInMB }
//	Reasons for Decision: 
//		Placement was scheduled because: 
//			New replicas needed to be placed.
//			Enough time has passed since the previous round of Placement.
//		
//		Balancing was not scheduled because: 
//			Balancing was not scheduled because there were no metrics that violated Metric Activity and Balancing Thresholds.
//		
//		Constraint Fixing was not scheduled because: 
//			Constraint Violation Fixing was not the most overdue phase needing scheduling.
//		
//		Between Placement, Constraint Violation Fixing, and Balancing, it was both necessary and the longest overdue.